// This build script is for an example project using GWT, Xtend and Auto-GWT, with Gradle and Eclipse.
// I couldn't find any complete Gradle samples online, so I wanted to share this.
// My notes for getting this project into a workable state, your milage may vary:
//
// @WebServlet annotations don't seem to work when using the Eclipse GWT plugin, even with 2.8.0-rc3 SDK, but work fine otherwise.
// Consider using either:
// - gradle gwtDev (requires hitting restart server if you change the server side)
// - A webserver external to the Eclipse GWT plugin
//
// To set up the project in Eclipse:
// - Download Eclipse for DSL developers
// - Consider installing the GWT plugin for Eclipse
//   - If you do, install the 2.8.0-rc3 SDK for the plugin, too
// - Consider using http://sdbg.github.io/ in Eclipse for Eclipse-integrated client-side debugging.
// - Run in the directory containing this build script: gradle eclipse
// - In Eclipse, use File > Import > General/Existing Projects into Workspace, and select this directory.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        classpath 'org.xtext:xtext-gradle-plugin:1.0.9'
    }
}

repositories {
    jcenter()
}

apply plugin: 'war'

apply plugin: 'org.xtext.xtend'
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'eclipse'
eclipseJdt << { file('.settings/org.eclipse.core.resources.prefs').write('eclipse.preferences.version=1\nencoding/<project>=utf-8') }

apply plugin: 'gwt'
gwt {
	gwtVersion='2.8.0-rc3'
    
	modules 'com.davidjozis.gwt.example.Example'
	
	maxHeapSize = "1024M"
}

group = 'com.davidjozis'
version = '0.0.1'

dependencies {
    // gwt dependencies are compile time only. compile is available at run time also.
    
    // disable transitive so we can use a later xtend version.
    gwt(group: 'org.xtend', name: 'auto-gwt', version: '0.0.1', classifier: 'sources') { transitive = false }
    gwt(group: 'org.xtend', name: 'auto-gwt', version: '0.0.1') { transitive = false }
    gwt group: 'org.eclipse.xtext', name: 'org.eclipse.xtext.xbase.lib.gwt', version: '2.10.0'
    compile group: 'org.eclipse.xtend', name: 'org.eclipse.xtend.lib.gwt', version: '2.10.0'
}
